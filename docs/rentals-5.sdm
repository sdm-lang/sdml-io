module rentals base <https://example.org/v/rentals> is

  import [ cids dc skos xsd ]

  ;; This is actually a US VIN
  import cids:VehicleIdentificationNumber

  @dc:version = 2

  datatype UniqueId <- xsd:string is
    @xsd:pattern = "[A-Za-z0-9_-]{22}"
  end

  enum RentalClass of
    SubCompact
    Compact
    FullSize
    CompactUtilityVehicle
    FullSizeUtilityVehicle
    Luxury
    Sports
  end

  enum CustomerLevel of
    Basic
    Silver
    Gold
  end

  entity Location

  entity Vehicle is
    @skos:prefLabel = "Vehicle"@en

    identity vin -> VehicleIdentificationNumber

    information -> VehicleInformation
    rentalClass -> {1} RentalClass
  end

  structure VehicleInformation is
    make -> xsd:string
    model -> xsd:string
    year -> xsd:integer is
      @xsd:minValue = 2000
      @xsd:maxValue = 3000
    end
  end

  entity Customer is
    identity id -> UniqueId
    name -> string
  end

  entity Booking is
    identity bookingCode -> unknown

    customer -> {1} Customer

    group
      @skos:prefLabel = "Rental Period"@en
      pickup_date -> xsd:date
      pickup_location -> Location
      dropoff_date -> xsd:date
      dropoff_location -> Location
    end

    group
      @skos:prefLabel = "Requested"@en
      bookingClass -> {1} RentalClass
      vehicle -> {0..1} Vehicle
    end
  end

  event VehicleAssigned source Booking is
    vehicle -> VehicleIdentificationNumber
  end

end
