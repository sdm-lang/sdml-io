module rentals is

  import [cids:VehicleIdentificationNumber cids:Uuid]
  import [xsd:date xsd:string]

  structure Address

  entity Booking is
    assert dropoff_after_pickup =
      "Ensure the dropoff date is greater than or equal to the pickup date."

    identity bookingCode -> unknown

    bookingClass -> {1} RentalClass
    vehicle -> {0..1} Vehicle
    pickup -> {1} Location
    pickupDate -> {1} xsd:date
    dropoff -> {1} Location
    dropoffDate -> {1} xsd:date
    customer -> {1} Customer
  end

  entity Customer is
    assert drivers_license_valid =
      "Ensure the drivers license date is in the future."

    identity id -> cids:Uuid

    contactAddress -> {1} Address
    driverLicense -> {0..1} LicenseInformation
  end

  structure LicenseInformation is
    number -> xsd:string
    state -> xsd:string
    expiration -> xsd:date
  end

  entity Location is
    identity id -> cids:Uuid

    address -> {1} Address
  end

  enum RentalClass

  entity Vehicle is
    identity vin -> cids:VehicleIdentificationNumber

    rentalClass -> {1} RentalClass
  end

end
